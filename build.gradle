buildscript {
    repositories {
        mavenLocal()
        mavenCentral()
    }
    dependencies {
        classpath 'net.saliman:gradle-cobertura-plugin:2.2.6'
        classpath 'org.kt3k.gradle.plugin:coveralls-gradle-plugin:2.4.0'
    }
}

plugins {
    id 'groovy'
    id 'eclipse'
    id 'com.jfrog.bintray' version '0.5'
}

apply plugin:'application'
apply plugin: 'cobertura'
apply plugin: 'com.github.kt3k.coveralls'

cobertura {
    coverageSourceDirs = sourceSets.main.groovy.srcDirs
    coverageFormats = ['html', 'xml']
    coverageIgnoreTrivial = true
    coverageIgnores = ['org.slf4j.Logger.*']
    coverageReportDir = new File("$buildDir/reports/cobertura")
}

mainClassName = "br.com.bluesoft.bee.Bee"

group = 'br.com.bluesoft.bee'
def artifact = 'bee'
version = '1.53'

def startScriptsDir = new File("$buildDir/scripts")

repositories {
	mavenCentral()
	mavenLocal()
}

dependencies {
	compile 'org.codehaus.groovy:groovy-all:2.2.0'
	compile group: 'org.codehaus.jackson', name: 'jackson-mapper-lgpl', version: '1.5.6'
	compile group: 'commons-cli', name: 'commons-cli', version: '1.2'
	testCompile "org.spockframework:spock-core:0.7-groovy-2.0"
	testCompile group: 'cglib', name: 'cglib-nodep', version: '2.2'
	testCompile group: 'org.mockito', name: 'mockito-core', version: '1.8.5'
}

startScripts.doLast {
	def scriptsDir = new File("${startScriptsDir}")
	scriptsDir.eachFile {
		def script = it
		def scriptText = script.text
		scriptText = scriptText.replaceAll('CLASSPATH=.*', 'for i in \\$APP_HOME/lib/*;do CLASSPATH=\\$CLASSPATH:\\$i; done')
		script.write scriptText
	}
}

jar {
	manifest {
		attributes 'Implementation-Title': "${artifact}", 'Implementation-Version': "${version}"
	}
}

applicationDistribution.from('bee.properties') {
	into 'bin'
}

test.finalizedBy(project.tasks.cobertura)
project.tasks.generateCoberturaReport.finalizedBy(project.tasks.coveralls)
