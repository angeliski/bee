
plugins {
    id 'groovy'
    id 'eclipse'
    id 'net.saliman.cobertura' version '2.2.5'
    id 'com.github.kt3k.coveralls' version '2.0.1'
    id 'com.jfrog.bintray' version '0.5'
}

apply plugin:'application'


mainClassName = "br.com.bluesoft.bee.Bee"

group = 'br.com.bluesoft.bee'
def artifact = 'bee'
version = '1.53'

def startScriptsDir = new File("$buildDir/scripts")

repositories {
	mavenCentral()
	mavenLocal()
}

dependencies {
	compile 'org.codehaus.groovy:groovy-all:2.2.0'
	compile group: 'org.codehaus.jackson', name: 'jackson-mapper-lgpl', version: '1.5.6'
	compile group: 'commons-cli', name: 'commons-cli', version: '1.2'
	testCompile "org.spockframework:spock-core:0.7-groovy-2.0"
	testCompile group: 'cglib', name: 'cglib-nodep', version: '2.2'
	testCompile group: 'org.mockito', name: 'mockito-core', version: '1.8.5'
}

startScripts.doLast {
	def scriptsDir = new File("${startScriptsDir}")
	scriptsDir.eachFile {
		def script = it
		def scriptText = script.text
		scriptText = scriptText.replaceAll('CLASSPATH=.*', 'for i in \\$APP_HOME/lib/*;do CLASSPATH=\\$CLASSPATH:\\$i; done')
		script.write scriptText
	}
}

jar {
	manifest {
		attributes 'Implementation-Title': "${artifact}", 'Implementation-Version': "${version}"
	}
}

applicationDistribution.from('bee.properties') {
	into 'bin'
}

cobertura.coverageFormats = ['html', 'xml']